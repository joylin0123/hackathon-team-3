data "archive_file" "api" {
  type        = "zip"
  source_file = "${path.module}/../lambdas/api/dist/index.js"
  output_path = "${path.module}/.build/api.zip"
}

resource "aws_lambda_function" "api" {
  function_name    = "hackathon-api"
  role             = aws_iam_role.api.arn
  handler          = "index.handler"
  runtime          = "nodejs22.x"
  timeout          = 30
  filename         = data.archive_file.api.output_path
  source_code_hash = data.archive_file.api.output_base64sha256

  environment {
    variables = {
      DYNAMO_TABLE_NAME = aws_dynamodb_table.telemetry.name
    }
  }
}

resource "aws_iam_role" "api" {
  name = "hackathon-api-role"

  assume_role_policy = jsonencode({
    Version = "2012-10-17"
    Statement = [
      {
        Action = "sts:AssumeRole"
        Effect = "Allow"
        Principal = {
          Service = "lambda.amazonaws.com"
        }
      }
    ]
  })
}

resource "aws_iam_role_policy_attachment" "api_logs" {
  role       = aws_iam_role.api.name
  policy_arn = "arn:aws:iam::aws:policy/service-role/AWSLambdaBasicExecutionRole"
}

resource "aws_iam_role_policy" "api_dynamo" {
  name = "hackathon-api-dynamo"
  role = aws_iam_role.api.id

  policy = jsonencode({
    Version = "2012-10-17"
    Statement = [
      {
        Action   = ["dynamodb:Query", "dynamodb:Scan", "dynamodb:GetItem"]
        Effect   = "Allow"
        Resource = aws_dynamodb_table.telemetry.arn
      }
    ]
  })
}

resource "aws_apigatewayv2_api" "api" {
  name          = "hackathon-api"
  protocol_type = "HTTP"

  cors_configuration {
    allow_origins = ["http://localhost:5173", "http://${aws_s3_bucket_website_configuration.frontend.website_endpoint}"]
    allow_methods = ["GET", "OPTIONS"]
    allow_headers = ["Content-Type"]
  }
}

resource "aws_apigatewayv2_stage" "api" {
  api_id      = aws_apigatewayv2_api.api.id
  name        = "$default"
  auto_deploy = true
}

resource "aws_apigatewayv2_integration" "api" {
  api_id                 = aws_apigatewayv2_api.api.id
  integration_type       = "AWS_PROXY"
  integration_uri        = aws_lambda_function.api.invoke_arn
  payload_format_version = "2.0"
}

resource "aws_apigatewayv2_route" "api_hello" {
  api_id    = aws_apigatewayv2_api.api.id
  route_key = "GET /api/hello"
  target    = "integrations/${aws_apigatewayv2_integration.api.id}"
}

resource "aws_apigatewayv2_route" "api_devices" {
  api_id    = aws_apigatewayv2_api.api.id
  route_key = "GET /api/devices"
  target    = "integrations/${aws_apigatewayv2_integration.api.id}"
}

resource "aws_apigatewayv2_route" "api_telemetry_latest" {
  api_id    = aws_apigatewayv2_api.api.id
  route_key = "GET /api/telemetry/latest"
  target    = "integrations/${aws_apigatewayv2_integration.api.id}"
}

resource "aws_apigatewayv2_route" "api_telemetry" {
  api_id    = aws_apigatewayv2_api.api.id
  route_key = "GET /api/telemetry"
  target    = "integrations/${aws_apigatewayv2_integration.api.id}"
}

resource "aws_apigatewayv2_route" "api_live" {
  api_id    = aws_apigatewayv2_api.api.id
  route_key = "GET /api/live"
  target    = "integrations/${aws_apigatewayv2_integration.api.id}"
}

resource "aws_apigatewayv2_route" "api_analytics_summary" {
  api_id    = aws_apigatewayv2_api.api.id
  route_key = "GET /api/analytics/summary"
  target    = "integrations/${aws_apigatewayv2_integration.api.id}"
}

resource "aws_apigatewayv2_route" "api_analytics_heatmap" {
  api_id    = aws_apigatewayv2_api.api.id
  route_key = "GET /api/analytics/heatmap"
  target    = "integrations/${aws_apigatewayv2_integration.api.id}"
}

resource "aws_apigatewayv2_route" "api_analytics_events" {
  api_id    = aws_apigatewayv2_api.api.id
  route_key = "GET /api/analytics/events"
  target    = "integrations/${aws_apigatewayv2_integration.api.id}"
}

resource "aws_apigatewayv2_route" "api_analytics_runs" {
  api_id    = aws_apigatewayv2_api.api.id
  route_key = "GET /api/analytics/runs"
  target    = "integrations/${aws_apigatewayv2_integration.api.id}"
}

resource "aws_apigatewayv2_route" "api_analytics_sensor_health" {
  api_id    = aws_apigatewayv2_api.api.id
  route_key = "GET /api/analytics/sensor-health"
  target    = "integrations/${aws_apigatewayv2_integration.api.id}"
}

resource "aws_lambda_permission" "api_gw" {
  statement_id  = "AllowAPIGatewayInvoke"
  action        = "lambda:InvokeFunction"
  function_name = aws_lambda_function.api.function_name
  principal     = "apigateway.amazonaws.com"
  source_arn    = "${aws_apigatewayv2_api.api.execution_arn}/*/*"
}
